wizard InsertProcedure {

	// if selected element is Link then only execute the wizard 
	guard : self.isKindOf(Link)

	// sub-menu title
	title : 'Insert Procedure'

	do{
	
		// asking user to enter name for new Procedure
		var newName : String;
		var message : String := 'Please provide a name for new Procedure';
		newName := UserInput.prompt(message);
		
		// storing old Procedure ( which is currently destination for selected link )for further use
		var oldDestination = self.destination;
		
		// don't create Procedure if name enter is empty
		if(newName = ''){return;}
		
		// creating new Procedure object and giving name which is enter by user 
		var procedureClass = self.eClass().ePackage.getEClassifier('Procedure');
		var procedureObject = self.eClass().ePackage.eFactoryInstance.create(procedureClass);
		procedureObject.name := newName;
		
		// doing link connection rearrangement
		self.destination := procedureObject;
		procedureObject.inputLinks.add(self);
		
		// creating new link
		var linkClass = self.eClass().ePackage.getEClassifier('Link');
		var linkObject = self.eClass().ePackage.eFactoryInstance.create(linkClass);
		
		// connecting newly created link between newly created Procedure and older one 
		procedureObject.outputLinks.add(linkObject);
		linkObject.destination := oldDestination;
		oldDestination.inputLinks.add(linkObject);
		
		// adding to new object to container 
		self.eContainer.eContainer.elements.add(procedureObject);
	}
}