wizard InsertWhileLoop {

	// if selected element is Link then only execute the wizard 
	guard : self.isKindOf(Link)

	// sub-menu title
	title : 'Insert WhileLoop'

	do{
	
		// asking user to enter name for new WhileLoop
		var newName : String;
		var message : String := 'Please provide a name for new WhileLoop';
		newName := UserInput.prompt(message);
		
		// storing old WhileLoop ( which is currently destination for selected link )for further use
		var oldDestination = self.destination;
		
		// don't create WhileLoop if name enter is empty
		if(newName = ''){return;}
		
		// creating new WhileLoop object and giving name which is enter by user 
		var whileLoopClass = self.eClass().ePackage.getEClassifier('WhileLoop');
		var whileLoopObject = self.eClass().ePackage.eFactoryInstance.create(whileLoopClass);
		whileLoopObject.name := newName;
		
		// doing link connection rearrangement
		self.destination := whileLoopObject;
		whileLoopObject.inputLinks.add(self);
		
		// creating new link
		var linkClass = self.eClass().ePackage.getEClassifier('Link');
		var linkObject = self.eClass().ePackage.eFactoryInstance.create(linkClass);
		
		// connecting newly created link between newly created WhileLoop and older one 
		whileLoopObject.outputLinks.add(linkObject);
		linkObject.destination := oldDestination;
		oldDestination.inputLinks.add(linkObject);
		
		// adding to new object to container 
		self.eContainer.eContainer.elements.add(whileLoopObject);
	}
}