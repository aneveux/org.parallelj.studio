wizard ConvertToWhileLoop {

	// if selected element is Procedure or ForEachLoop or Handler and not WhileLoop or Block then only execute the wizard 
	guard : self.isKindOf(Procedure) and not self.eContainer.isKindOf(Block) and not self.isKindOf(WhileLoop) and not self.isKindOf(Block)  

	// sub-menu title
	title : 'Turn into WhileLoop'

	do {
		var isOK : Boolean := true;

		if(self.isKindOf(ForEachLoop)){
			isOK = UserInput.confirm('This wizard may result in the loss of some data from the former object. Do you confirm ?');
		}
		
		if (isOK) 
		{	
			// creating new WhileLoop object 
			var whileLoopClass = self.eClass().ePackage.getEClassifier('WhileLoop');
			var whileLoopObject = self.eClass().ePackage.eFactoryInstance.create(whileLoopClass);
			
			// Assigning all attribute values to newly created WhileLoop object
			whileLoopObject.name = self.name;
			whileLoopObject.description = self.description;
			whileLoopObject.executable = self.executable;
			whileLoopObject.join = self.join;
			whileLoopObject.split= self.split;
			whileLoopObject.inputLinks.addAll(self.inputLinks);
			whileLoopObject.outputLinks.addAll(self.outputLinks);
			
			if(self.isKindOf(ForEachLoop)){
				whileLoopObject.executionMode = self.executionMode;
			}
			
			// adding to new object to container
			self.eContainer.elements.add(whileLoopObject);
			// removing to selected old object from container
			self.eContainer.elements.remove(self);
		}
	}
}